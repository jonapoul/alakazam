apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdkVersion versions.targetSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.libVersionCode
        versionName versions.libVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
            '-Xjvm-default=compatibility',
            '-Xopt-in=kotlin.RequiresOptIn'
        ]
    }
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId 'com.github.jonapoul.android-common'
                artifactId ARTIFACT_ID
                version versions.libVersionName
                from components.release
            }
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "androidx.activity") {
            details.useVersion versions.activity_ktx
        }
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$versions.desugar"

    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-android-compiler:$versions.hilt"

    testImplementation project(":testing-core")
}
