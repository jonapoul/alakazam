buildscript {
    apply from: "$rootProject.projectDir/versions.gradle"

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradle"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotless"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$versions.ktlint"
    }
}

allprojects {
    apply from: "$rootProject.projectDir/versions.gradle"

    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: "org.jetbrains.dokka"
    dokkaGfm.configure {
        outputDirectory.set(new File("${rootProject.rootDir}/docs/api"))
    }

    apply plugin: "org.jlleitschuh.gradle.ktlint"
    ktlint {
        disabledRules = ["no-wildcard-imports"]
    }

    apply plugin: "com.diffplug.spotless"
    spotless {
        format 'misc', {
            target '**/*.gradle', '**/*.md', '**/.gitignore', '**/.pro'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
        kotlin {
            ktlint()
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
        json {
            target '**/*.json'
            simple()
        }
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'FULL'
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }

    afterEvaluate {
        tasks.getByName('build').dependsOn dokkaGfm
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
