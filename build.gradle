buildscript {
    apply from: "$rootProject.projectDir/versions.gradle"

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"
        classpath "com.android.tools.build:gradle:$versions.gradle"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotless"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$versions.detekt"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$versions.ktlint"
    }
}

allprojects {
    def root = rootProject.projectDir
    apply from: "$root/versions.gradle"

    apply plugin: "io.gitlab.arturbosch.detekt"
    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$versions.detekt"
    }
    detekt {
        toolVersion = versions.detekt
        config = files("$rootDir/detekt.yml")
        buildUponDefaultConfig = true
        reports.html.enabled = true
    }

    apply plugin: "org.jetbrains.dokka"
    tasks.named("dokkaHtml") {
        outputDirectory.set(new File("${rootProject.rootDir}/docs/api"))
        dokkaSourceSets {
            configureEach {
                includeNonPublic.set(false)
                skipEmptyPackages.set(true)
                reportUndocumented.set(true)
                noAndroidSdkLink.set(false)
            }
        }
    }

    apply plugin: "org.jlleitschuh.gradle.ktlint"
    ktlint {
        reporters {
            reporter "html"
        }
    }

    apply plugin: "com.diffplug.spotless"
    spotless {
        format 'misc', {
            target '**/*.gradle', '**/*.md', '**/.gitignore', '**/.pro'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'FULL'
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
        }
    }

    afterEvaluate {
        // Attach the dokka task to build afterwards, since the build task doesn't exist right now.
        tasks.getByName("build").dependsOn "dokkaHtml"
    }
}
