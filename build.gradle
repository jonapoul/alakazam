buildscript {
    apply from: "$rootProject.projectDir/versions.gradle"

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"
        classpath "com.android.tools.build:gradle:$versions.gradle"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotless"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$versions.detekt"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$versions.ktlint"
    }
}

allprojects {
    def root = rootProject.projectDir
    apply from: "$root/versions.gradle"
    apply from: "$root/gradle/plugins/detekt.gradle"
    apply from: "$root/gradle/plugins/dokka.gradle"
    apply from: "$root/gradle/plugins/ktlint.gradle"
    apply from: "$root/gradle/plugins/spotless.gradle"

    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'FULL'
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
        }
    }

    afterEvaluate {
        // Attach the dokka task to build afterwards, since the build task doesn't exist right now.
        tasks.getByName('build').dependsOn "dokkaHtml"
    }
}
